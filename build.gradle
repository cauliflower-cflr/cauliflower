/***********
 * Plugins *
 ***********/

apply plugin: 'java';
apply plugin: 'antlr';
apply plugin: 'application';


/********************
 * Project Settings *
 ********************/

group 'cauliflower';
sourceCompatibility = 1.8;
version = getVersionNameFromGit();
mainClassName = 'cauliflower.Main';
defaultTasks 'installDist';


/****************
 * Repositories *
 ****************/
repositories {
    mavenCentral()
}


/****************
 * Dependencies *
 ****************/
dependencies {
    testCompile 'junit:junit:4.12';
    compile "com.beust:jcommander:1.48";
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5';
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.5';
    antlr "org.antlr:antlr4:4.5"; // use ANTLR version 4

}

test {
    testLogging {
        events "passed", "skipped", "failed";
    }
}

processResources {
    expand projectVersion: project.version,
           curDate: new Date().format( 'yyyy-MM-dd' );
}

generateGrammarSource {
    arguments += ["-visitor", "-package", "cauliflower.parser.grammar"];
}


/************
 * Artefact *
 ************/

jar {
    manifest {
        attributes(
                'version' : version,
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'cauliflower.Main'
                );
    }
}


/****************
 * Miscelaneous *
 ****************/

def getVersionNameFromGit() {
    return "git describe --tags --match [0-9]\\.[0-9] --always".execute().text.trim();
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'; // A bug in the antlr plugin was fixed in 2.7
}
